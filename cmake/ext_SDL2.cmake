include(cmake/build_ext_project.cmake)

set(SDL2_CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/install/sdl2" CACHE PATH "Path to a directory where SDL2 library in going to be installed")

set(CMAKE_C_COMPILER_ "${CMAKE_C_COMPILER}")
set(CMAKE_CXX_COMPILER_ "${CMAKE_CXX_COMPILER}")
# workaround spaces in path to windows compiler
if(WIN32)
    set(CMAKE_C_COMPILER_ "\"${CMAKE_C_COMPILER}\"")
    set(CMAKE_CXX_COMPILER_ "\"${CMAKE_CXX_COMPILER}\"")
endif()

# CMAKE_OSX_ARCHITECTURES can contain semicolons.
# Semicolons are problematic when passed to externalproject_add
string(REPLACE ";" "$<SEMICOLON>" CMAKE_OSX_ARCHITECTURES_ "${CMAKE_OSX_ARCHITECTURES}")

list(APPEND SDL2_EXT_CMAKE_ARGS
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_C_COMPILER:STRING=${CMAKE_C_COMPILER_}
    -DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER_}
    -DCMAKE_INSTALL_PREFIX:PATH=${SDL2_CMAKE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR=lib
    -DSDL_FORCE_STATIC_VCRT=ON
    -DSDL_SHARED_ENABLED_BY_DEFAULT=OFF
    -DSDL_AUDIO_ENABLED_BY_DEFAULT=OFF
    -DSDL_STATIC_ENABLED_BY_DEFAULT=ON
    -DSDL_LIBC=ON
    -DSDL_HIDAPI_DISABLED=ON
)

build_external_project(SDL2 "${CMAKE_SOURCE_DIR}/engine/third_party/SDL2-2.0.22.zip" ${SDL2_EXT_CMAKE_ARGS})

find_package(SDL2 REQUIRED PATHS "${SDL2_CMAKE_INSTALL_PREFIX}" NO_DEFAULT_PATH)
